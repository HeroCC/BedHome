plugins {
  id "org.zeroturnaround.gradle.jrebel" version '1.1.3'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'java'

String packagePath = 'org.minecast.bedhome'
group packagePath
version = '2.29'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  mavenCentral()
  maven { url "https://hub.spigotmc.org/nexus/content/groups/public" }
  maven { url "http://repo.gravitydevelopment.net" }
  maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
  maven { url 'https://repo.bstats.org/content/repositories/releases' }
}

dependencies {
  compile group: 'net.gravitydevelopment.updater', name: 'updater', version: '2.4'
  compile group: 'org.bukkit', name: 'bukkit', version: '1.13-R0.1-SNAPSHOT'
  compile group: 'net.milkbowl.vault', name: 'VaultAPI', version: '1.6'
  compile group: 'org.bstats', name: 'bstats-bukkit', version: '1.1'
}

artifacts { // task 'build' runs generates uberjar
  archives shadowJar
}

//noinspection GroovyAssignabilityCheck
jar {
  version = version + '-min' // we want the Uberjar to be distributed, this is the minified version
  dependsOn generateRebel // Automatically generate jRebel configs
}

shadowJar {
  dependencies {
    include dependency('net.gravitydevelopment.updater:updater')
    include dependency('org.bstats:bstats-bukkit')
  }
  relocate('org.bstats', packagePath + '.dependencies')
  relocate('net.gravitydevelopment.updater', packagePath + '.dependencies')
  classifier = null
  dependsOn generateRebel
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

ext {
  resourceTokens = [
      'Version': version
  ]
}

processResources {
  filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}


